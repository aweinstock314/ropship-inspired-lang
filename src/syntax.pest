ident = { (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
numeric_literal = { ASCII_DIGIT+ }

type_id = { pointertype | primword64 }
    pointertype = { "ptr" ~ type_id }
    primword64 = { "word64" }

vardecl = { ident ~ ":" ~ type_id }

argument_list = { ((vardecl ~ ",")* ~ vardecl) ? }

function = { "fn" ~ ident ~ "(" ~ argument_list ~ ")" ~ "->" ~ type_id ~ "{" ~ statement* ~ "}" }

expr = { var_expr | literal_expr | deref_expr }
    var_expr = { ident }
    literal_expr = { numeric_literal }
    deref_expr = { "*" ~ expr }

compound_assign = { "=" | "+=" }

statement = { localdef | assignment | dotimes | returnstmt }
    localdef = { "local" ~ vardecl ~ "=" ~ expr ~ ";" }
    assignment = { ident ~ compound_assign ~ expr ~ ";" }
    dotimes = { "dotimes" ~ expr ~ "{" ~ statement* ~ "}" }
    returnstmt = { "return" ~ expr ~ ";" }

WHITESPACE = _{ " " | "\t" | "\n" }
